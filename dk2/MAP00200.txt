REM ********************************************
REM
REM             Script for Level 200
REM		Smilesville
REM
REM ********************************************

LEVEL_VERSION(1)
SET_GENERATE_SPEED(200)
MAX_CREATURES(PLAYER0,12)
START_MONEY(PLAYER0,5000)

ADD_CREATURE_TO_POOL(FIREFLY,2)
ADD_CREATURE_TO_POOL(GOBLIN,10)

CREATURE_AVAILABLE(PLAYER0,FIREFLY,1,0)
CREATURE_AVAILABLE(PLAYER0,GOBLIN,1,0)

MAGIC_AVAILABLE(PLAYER0,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)

SET_CREATURE_FEAR_WOUNDED(TUNNELLER,0)
SET_CREATURE_FEAR_WOUNDED(KNIGHT,0)

CREATE_PARTY(KNIGHT)
    ADD_TO_PARTY(KNIGHT,KNIGHT,1,1000,ATTACK_ENEMIES,0)

CONCEAL_MAP_RECT(Player0, 5, 5, 250, 250,1)
REVEAL_MAP_LOCATION(PLAYER0,2,6)

SET_CREATURE_STRENGTH(KNIGHT,100)
SET_CREATURE_ARMOUR(KNIGHT,70)
SET_CREATURE_STRENGTH(TUNNELLER,20)
SET_CREATURE_ARMOUR(TUNNELLER,20)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)

SET_GAME_RULE(ImpWorkExperience,124)
SET_GAME_RULE(TrainingRoomMaxLevel,4)

SET_TIMER(PLAYER0,TIMER0)

IF(PLAYER0,TIMER0 >= 10)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe01.mp3")
ENDIF

IF(PLAYER0,TIMER0 >= 250)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe02.mp3")
ENDIF

DISPLAY_OBJECTIVE(2,PLAYER0)

IF(PLAYER0,TIMER0 >= 400)
	DISPLAY_INFORMATION(1,PLAYER0)
ENDIF

IF(PLAYER0,TIMER0 >= 800)
	DISPLAY_INFORMATION(9,PLAYER0)
ENDIF

IF(PLAYER0, GOBLIN >= 1)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe05.ogg")
ENDIF

IF(PLAYER0, FIREFLY >= 1)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe06.ogg")
ENDIF

IF(PLAYER0,LAIR >= 15)
	DISPLAY_INFORMATION(4,PLAYER0)
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 1)
        IF(PLAYER0,GARDEN >= 9)
                SET_TIMER(PLAYER0,TIMER1)
                SET_FLAG(PLAYER0,FLAG0,4)
        ENDIF
ENDIF


REM *** BUILT GARDEN ***
IF(PLAYER0,FLAG0 == 4)
    IF(PLAYER0,TOTAL_CREATURES >= 6)
        SET_FLAG(PLAYER0,FLAG0,5)
    ENDIF
ENDIF

REM *** HAS SEVEN OR MORE CREATURES ***
IF(PLAYER0,FLAG0 == 5)
    IF(PLAYER0,TIMER1 >= 600)
        REM CREATE_TEXT(30, 16)

        IF(PLAYER0,TIMER1 >= 800)
    	    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe03.mp3")
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON,0,1,300)
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON,0,1,300)
            DISPLAY_OBJECTIVE(5,PLAYER0)
            TUTORIAL_FLASH_BUTTON(37,-1)
            IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
                REM CREATE_TEXT(13, 18)
                DISPLAY_OBJECTIVE(6,PLAYER0)
                SET_FLAG(PLAYER0,FLAG0,6)
            ENDIF
        ENDIF
    ENDIF
ENDIF



REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track01.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
    REM Time battle music been playing
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
    REM Time battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
    REM Time no battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
    IF(PLAYER3,FLAG5 != 4)
        NEXT_COMMAND_REUSABLE
        SET_MUSIC("battle01.mp3")
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER3,FLAG5,4)
    ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
    IF(PLAYER3,TIMER5 > 100)
        IF(PLAYER3,FLAG5 != 2)
            NEXT_COMMAND_REUSABLE
            SET_MUSIC("dk2track01.mp3")
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER3,FLAG5,2)
        ENDIF
    ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
   		IF(PLAYER0,IMP <= 4)
        		NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
        		NEXT_COMMAND_REUSABLE
        		SET_TIMER(PLAYER0,TIMER2)
		ENDIF
ENDIF 


REM *** HAS WON FIRST BATTLE ***
IF(PLAYER0,FLAG0 == 6)
        IF(PLAYER0,TOTAL_CREATURES >= 6)
    	    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe04.mp3")
            ADD_PARTY_TO_LEVEL(PLAYER_GOOD,KNIGHT,-1,1)
            REM CREATE_TEXT(14, 19)
            DISPLAY_OBJECTIVE(7,PLAYER0)
			DISPLAY_MESSAGE(7, KNIGHT)
            SET_FLAG(PLAYER0,FLAG0,7)
        ENDIF
ENDIF

IF(PLAYER0,FLAG0 == 7)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        REM CREATE_TEXT(15, 20)
        DISPLAY_OBJECTIVE(8,PLAYER0)
        WIN_GAME
    ENDIF
ENDIF

