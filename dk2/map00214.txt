REM ********************************************
REM
REM             Script for Level 214
REM
REM ********************************************

LEVEL_VERSION(1)

SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0, 35)

START_MONEY(PLAYER0, 250000)

ADD_CREATURE_TO_POOL(SORCEROR, 5)
ADD_CREATURE_TO_POOL(TROLL, 5)
ADD_CREATURE_TO_POOL(SALAMANDER, 5)
ADD_CREATURE_TO_POOL(GOBLIN, 5)
ADD_CREATURE_TO_POOL(DARK_MISTRESS, 5)
ADD_CREATURE_TO_POOL(BILE_DEMON, 5)
ADD_CREATURE_TO_POOL(BLACK_KNIGHT, 5)
ADD_CREATURE_TO_POOL(DARK_ELF,5)

SET_GAME_RULE(TrainingRoomMaxLevel,4)

IF(PLAYER0, BARRACKS >= 8)
	SET_GAME_RULE(TrainingRoomMaxLevel,8)
ENDIF

CREATURE_AVAILABLE(ALL_PLAYERS, BLACK_KNIGHT, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, SORCEROR, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, SALAMANDER, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, TROLL, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, DARK_MISTRESS, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, GOBLIN, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, BILE_DEMON, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, DARK_ELF,1,0)

ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, WORKSHOP, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, STONE_BRIDGE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TORTURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GUARD_POST, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, PRISON, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TEMPLE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, BARRACKS, 4, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HEAL_CREATURE, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CALL_TO_ARMS, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_CAVE_IN, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HOLD_AUDIENCE, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SPEED, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_DISEASE, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, STEEL, 1, 0)
DOOR_AVAILABLE(ALL_PLAYERS, MAGIC, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, TURRET, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, POISON_GAS, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, BOULDER, 1, 0)

SET_GAME_RULE(ImpWorkExperience,124)

CONCEAL_MAP_RECT(Player0, 127, 127, 250, 250,1)
REVEAL_MAP_LOCATION(PLAYER0,PLAYER0,-1)
REVEAL_MAP_LOCATION(PLAYER0,8,2)

REVEAL_MAP_LOCATION(PLAYER0,9,6)
REVEAL_MAP_LOCATION(PLAYER0,10,6)
REVEAL_MAP_LOCATION(PLAYER0,11,6)
REVEAL_MAP_LOCATION(PLAYER0,7,6)

SET_GAME_RULE(MaxThingsInHand,30)
SET_HAND_RULE(PLAYER0,ANY_CREATURE,RULE1,DENY,DROPPED_TIME_LOWER,60)

if(player0,ENTRANCE == 9)
  next_command_reusable
  max_creatures(Player0,15)
ENDIF
if(player0,ENTRANCE == 18)
  next_command_reusable
  max_creatures(Player0,20)
ENDIF
if(player0,ENTRANCE == 27)
  next_command_reusable
  max_creatures(Player0,25)
ENDIF
if(player0,ENTRANCE == 36)
  next_command_reusable
  max_creatures(Player0,30)
ENDIF

CREATE_PARTY(P1A)
ADD_TO_PARTY(P1A, WITCH, 2, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1A, FAIRY, 1, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1A, FAIRY, 1, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1A, FAIRY, 1, 30, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(P1B)
ADD_TO_PARTY(P1B, WITCH, 2, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1B, FAIRY, 1, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1B, FAIRY, 1, 30, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(P1B, WITCH, 2, 30, ATTACK_DUNGEON_HEART, 0)

REM INTRO

IF(PLAYER0,TIMER4 >= 20)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl15spe01.ogg")
	QUICK_OBJECTIVE(90,"Listen well. There's talk of creatures being rallied for a Dark Crusade.")
ENDIF

IF(PLAYER0, KNIGHT >= 1)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl15spe02.ogg")
	QUICK_OBJECTIVE(91,"At last the Black Knight realises that honour, justice and piety are just plain boring. So he joins your evil cause, and that allows him plenty time for pillaging and killing: such pure sport becomes him.")
ENDIF

IF(PLAYER0,BARRACKS >= 1)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl15spe03.ogg")
	QUICK_OBJECTIVE(92,"The Combat Pit. This room is an arena where your creatures prove their prowess in such martial sports. Place two or more therein and watch them fight each other unto death. And thus the victor's trained to higher levels in its combat skills.")
ENDIF

RUN_AFTER_VICTORY(1)

CREATE_PARTY(HORNY)
    ADD_TO_PARTY(HORNY,EVILLORD,10,0,SNIPE_DUNGEON_HEART,-370)

COMPUTER_PLAYER(PLAYER6,ROAMING)
ALLY_PLAYERS(PLAYER0,PLAYER6,3)
ALLY_PLAYERS(PLAYER6,PLAYER_GOOD,3)
SET_PLAYER_COLOR(PLAYER6,RED)
SET_TIMER(PLAYER0,TIMER4)

IF(PLAYER0,TIMER4 >= 30)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,0)
    USE_POWER_AT_LOCATION(PLAYER0,5,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER4 >= 40)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 50)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 60)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 70)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 80)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 90)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 100)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 110)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 120)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 130)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 140)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 150)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 160)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 170)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 180)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER4 >= 190)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF

IF(PLAYER0,TIMER4 >= 200)
    ZOOM_TO_LOCATION(PLAYER0,PLAYER0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,10,6)
    USE_POWER_AT_LOCATION(PLAYER0,5,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER4 >= 210)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,51)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,896,6)
ENDIF

REM SETTINGS

REM hero gate 1

SET_FLAG(PLAYER_GOOD, FLAG0, 0)
SET_TIMER(PLAYER0, TIMER0)
IF(PLAYER_GOOD, FLAG0 == 0)
	IF(PLAYER0, TIMER0 >= 2725)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, P1A, -1, 1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0, TIMER0)
	ENDIF
ENDIF

IF_SLAB_OWNER(69,51,PLAYER0)
	IF_SLAB_OWNER(70,51,PLAYER0)
		IF_SLAB_OWNER(71,51,PLAYER0)
			IF_SLAB_OWNER(72,51,PLAYER0)
				IF_SLAB_OWNER(69,54,PLAYER0)
					IF_SLAB_OWNER(70,54,PLAYER0)
						IF_SLAB_OWNER(71,54,PLAYER0)
							IF_SLAB_OWNER(72,54,PLAYER0)
								IF_SLAB_OWNER(72,52,PLAYER0)
									IF_SLAB_OWNER(72,53,PLAYER0)
										IF_SLAB_OWNER(69,52,PLAYER0)
											IF_SLAB_OWNER(69,53,PLAYER0)
												SET_FLAG(PLAYER_GOOD, FLAG0, 1)
												CHANGE_SLAB_TYPE(70,52,BROKEN_ENTRANCE2x2,MATCH)
												HIDE_HERO_GATE(-1,1)
				                				ZOOM_TO_LOCATION(PLAYER0,-1)
												PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe08.ogg")
    	                  						QUICK_INFORMATION(26,"You've claimed the land around the Hero Gate and thus broken it. No more thieving Heroes will invade your realm this way!")
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,-1)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,-1,POWER_CAVE_IN,6,1)
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,1)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,1,POWER_CAVE_IN,6,1)
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,4)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,4,POWER_CAVE_IN,6,1)
												PLAY_MESSAGE(PLAYER0,SOUND,152)
											ENDIF
										ENDIF
									ENDIF
								ENDIF
							ENDIF
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

REM hero gate 2
SET_FLAG(PLAYER_GOOD, FLAG1, 0)
SET_TIMER(PLAYER0, TIMER1)
IF(PLAYER_GOOD, FLAG1 == 0)
	IF(PLAYER0, TIMER1 >= 3725)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, P1B, -2, 1)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0, TIMER1)
	ENDIF
ENDIF

IF_SLAB_OWNER(69,66,PLAYER0)
	IF_SLAB_OWNER(70,66,PLAYER0)
		IF_SLAB_OWNER(71,66,PLAYER0)
			IF_SLAB_OWNER(72,66,PLAYER0)
				IF_SLAB_OWNER(69,69,PLAYER0)
					IF_SLAB_OWNER(70,69,PLAYER0)
						IF_SLAB_OWNER(71,69,PLAYER0)
							IF_SLAB_OWNER(72,69,PLAYER0)
								IF_SLAB_OWNER(72,67,PLAYER0)
									IF_SLAB_OWNER(72,68,PLAYER0)
										IF_SLAB_OWNER(69,67,PLAYER0)
											IF_SLAB_OWNER(69,68,PLAYER0)
												SET_FLAG(PLAYER_GOOD, FLAG1, 1)
												CHANGE_SLAB_TYPE(70,67,BROKEN_ENTRANCE2x2,MATCH)
												HIDE_HERO_GATE(-2,1)
				                				ZOOM_TO_LOCATION(PLAYER0,-2)
												PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe08.ogg")
    	                  						QUICK_INFORMATION(26,"You've claimed the land around the Hero Gate and thus broken it. No more thieving Heroes will invade your realm this way!")
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,-2)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,-2,POWER_CAVE_IN,6,1)
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,2)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,2,POWER_CAVE_IN,6,1)
												CREATE_EFFECT(EFFECT_HARMLESS_GAS_1,12)
												USE_POWER_AT_LOCATION(PLAYER_GOOD,12,POWER_CAVE_IN,6,1)
												PLAY_MESSAGE(PLAYER0,SOUND,152)
											ENDIF
										ENDIF
									ENDIF
								ENDIF
							ENDIF
						ENDIF
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track02.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
    REM Time battle music been playing
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
    REM Time battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
    REM Time no battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
    IF(PLAYER3,FLAG5 != 4)
        NEXT_COMMAND_REUSABLE
        SET_MUSIC("battle03.mp3")
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER3,FLAG5,4)
    ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
    IF(PLAYER3,TIMER5 > 100)
        IF(PLAYER3,FLAG5 != 2)
            NEXT_COMMAND_REUSABLE
            SET_MUSIC("dk2track02.mp3")
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER3,FLAG5,2)
        ENDIF
    ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
   		IF(PLAYER0,IMP <= 4)
        		NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
        		NEXT_COMMAND_REUSABLE
        		SET_TIMER(PLAYER0,TIMER2)
		ENDIF
ENDIF 

IF(PLAYER_GOOD, KNIGHT == 0)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl15spe04.ogg")
	QUICK_OBJECTIVE(93,"Oh so fine, I must admit my joy to see such Fairy folk destroyed. With style and spectacle you play, yours will be fame on Judgement Day.")
	WIN_GAME
	ALLY_PLAYERS(PLAYER0,PLAYER5,3)
	ADD_OBJECT_TO_LEVEL(FAKE_GEM,LAST_DEATH_EVENT[PLAYER_GOOD],1,PLAYER5)
 	COMPUTER_PLAYER(PLAYER5,0)
 	SET_TIMER(PLAYER6,TIMER6)
ENDIF

HIDE_HERO_GATE(3,1)

IF(PLAYER6,TIMER6 >= 20)
    MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
ENDIF

IF(PLAYER6,TIMER6 >= 60)
        ADD_OBJECT_TO_LEVEL(HORNY_STATUE,-3,1,PLAYER0)
        ZOOM_TO_LOCATION(PLAYER0,-3)
        CREATE_EFFECT(EFFECT_FLASH,PLAYER0)
        CREATE_EFFECT(EFFECT_EXPLOSION_5,-3)
        CREATE_EFFECT(EFFECT_SPANGLE_RED,-3)
        CREATE_EFFECT(EFFECT_WORD_OF_POWER,-3)
ENDIF

IF(PLAYER6,TIMER6 >= 100)
        ADD_PARTY_TO_LEVEL(PLAYER6,HORNY,-3,1)
        SET_OBJECT_CONFIGURATION(HORNY_STATUE,MaximumSize,0)
ENDIF

IF(PLAYER5,HEART_HEALTH > 0)
    SET_FLAG(PLAYER0,FLAG0,8)
ENDIF
IF(PLAYER0,FLAG0 == 8)
    IF(PLAYER5,HEART_HEALTH < 3)
        SET_OBJECT_CONFIGURATION(FAKE_GEM,UpdateFunction,UPDATE_POWER_LIGHTNING)
        SET_TIMER(PLAYER6,TIMER7)
    ENDIF
    IF(PLAYER6,TIMER7 > 50)
        KILL_CREATURE(PLAYER6,EVILLORD,ANYWHERE,1)
    ENDIF
ENDIF