REM ********************************************
REM
REM             Script for Level 200
REM        Smilesville
REM
REM ********************************************

LEVEL_VERSION(1)
SET_GENERATE_SPEED(200)
MAX_CREATURES(PLAYER0,10)
START_MONEY(PLAYER0,5000)

ADD_CREATURE_TO_POOL(FIREFLY,2)
ADD_CREATURE_TO_POOL(GOBLIN,10)

CREATURE_AVAILABLE(PLAYER0,FIREFLY,1,0)
CREATURE_AVAILABLE(PLAYER0,GOBLIN,1,0)

MAGIC_AVAILABLE(PLAYER0,POWER_HAND,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SLAP,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_SIGHT,1,1)
MAGIC_AVAILABLE(PLAYER0,POWER_IMP,1,1)

SET_CREATURE_FEAR_WOUNDED(TUNNELLER,0)
SET_CREATURE_FEAR_WOUNDED(KNIGHT,0)
SET_CREATURE_FEAR_STRONGER(KNIGHT,10000)

CREATE_PARTY(KNIGHT)
    ADD_TO_PARTY(KNIGHT,KNIGHT,1,1000,ATTACK_ENEMIES,0)

SET_HAND_RULE(PLAYER0,ANY_CREATURE,RULE1,DENY,DROPPED_TIME_LOWER,60)

SET_CREATURE_STRENGTH(KNIGHT,100)
SET_CREATURE_ARMOUR(KNIGHT,70)
SET_CREATURE_STRENGTH(TUNNELLER,20)
SET_CREATURE_ARMOUR(TUNNELLER,20)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

ROOM_AVAILABLE(PLAYER0,LAIR,1,1)
ROOM_AVAILABLE(PLAYER0,GARDEN,1,1)

SET_GAME_RULE(TrainingRoomMaxLevel,4)

REM START INTRO FOR CHOOSING THE REVEAL/CONCEAL

QUICK_OBJECTIVE(0,"Please pick the box you prefer. You can choose between the DK1 or DK2 Map concealment option, and it will remain for the rest of the campaign.")

ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM_A,7,1,PLAYER0)
ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM_B,8,2,PLAYER0)
ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM,9,3,PLAYER0)

SET_OBJECT_CONFIGURATION(FAKE_HEART,MaximumSize,0)

SET_BOX_TOOLTIP(1,"DK2 Full concealment")

SET_FLAG(PLAYER_GOOD,FLAG5,0)

IF(PLAYER0,BOX1_ACTIVATED > 0)
    IF(PLAYER_GOOD,FLAG5 == 0)
        NEXT_COMMAND_REUSABLE
        CONCEAL_MAP_RECT(PLAYER0, 36, 48, 72, 96 ,1)
        NEXT_COMMAND_REUSABLE
        REVEAL_MAP_LOCATION(PLAYER0,2,6)
        NEXT_COMMAND_REUSABLE
        REVEAL_MAP_LOCATION(PLAYER0,5,18)
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER0,BOX1_ACTIVATED,0)
        NEXT_COMMAND_REUSABLE
        PLAY_MESSAGE(PLAYER0,SOUND,335)
        NEXT_COMMAND_REUSABLE
        ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM_A,7,1,PLAYER0)   
        NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER1,CAMPAIGN_FLAG1,1)
    ENDIF
ENDIF

SET_BOX_TOOLTIP(2,"DK1 concealment")

IF(PLAYER0,BOX2_ACTIVATED > 0)
    IF(PLAYER_GOOD,FLAG5 == 0)
        NEXT_COMMAND_REUSABLE
        REVEAL_MAP_RECT(PLAYER0,36,50,72,96)
        NEXT_COMMAND_REUSABLE
        CONCEAL_MAP_RECT(PLAYER0,36,48,72,96,0)
        NEXT_COMMAND_REUSABLE
        REVEAL_MAP_LOCATION(PLAYER0,PLAYER0,-1)
        NEXT_COMMAND_REUSABLE
         REVEAL_MAP_LOCATION(PLAYER0,2,6)
        NEXT_COMMAND_REUSABLE 
        SET_FLAG(PLAYER0,BOX2_ACTIVATED,0)
        NEXT_COMMAND_REUSABLE
        PLAY_MESSAGE(PLAYER0,SOUND,335)
        NEXT_COMMAND_REUSABLE
		REVEAL_MAP_LOCATION(PLAYER0,5,15)
        NEXT_COMMAND_REUSABLE
        ADD_OBJECT_TO_LEVEL(SPECBOX_CUSTOM_B,8,2,PLAYER0) 
 		SET_FLAG(PLAYER1,CAMPAIGN_FLAG1,0)
    ENDIF
ENDIF

SET_BOX_TOOLTIP(3,"Start the game")
SET_OBJECT_CONFIGURATION(SOUL_CONTAINER,Size_XY,100)
SET_OBJECT_CONFIGURATION(SOUL_CONTAINER,Size_Z,100)
IF(PLAYER0,BOX3_ACTIVATED > 0)
    SET_OBJECT_CONFIGURATION(SPECBOX_CUSTOM_A,UpdateFunction,UPDATE_POWER_LIGHTNING)
    SET_OBJECT_CONFIGURATION(SPECBOX_CUSTOM_B,UpdateFunction,UPDATE_POWER_LIGHTNING)
    SET_FLAG(PLAYER_GOOD,FLAG5,1)
    PLAY_MESSAGE(PLAYER0,SOUND,335)
    SET_TIMER(PLAYER0,TIMER0)
    CHANGE_SLAB_TYPE(10,22,HEART_PEDESTAL,MATCH)
    CHANGE_SLAB_TYPE(11,23,HEART_PEDESTAL,MATCH)
    ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,4,1,0)
    SET_TIMER(PLAYER0,TIMER2)
ENDIF
IF(PLAYER0,TIMER0 > 2)
    ADD_HEART_HEALTH(PLAYER0,-30000,0)
    ADD_OBJECT_TO_LEVEL(SOUL_CONTAINER,5,0,PLAYER0)
    SET_OBJECT_CONFIGURATION(SOUL_CONTAINER,Size_XY,768)
    SET_OBJECT_CONFIGURATION(SOUL_CONTAINER,Size_Z,768)
ENDIF

REM START INTRO

RUN_AFTER_VICTORY(1)

CREATE_PARTY(HORNY)
    ADD_TO_PARTY(HORNY,EVILLORD,10,0,SNIPE_DUNGEON_HEART,-370)

COMPUTER_PLAYER(PLAYER6,ROAMING)
ALLY_PLAYERS(PLAYER0,PLAYER6,3)
SET_PLAYER_COLOR(PLAYER6,RED)
REVEAL_MAP_LOCATION(PLAYER0,PLAYER0,-1)

IF(PLAYER0,TIMER0 >= 10)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAKELORD,-1,1,1,0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,0)
    USE_POWER_AT_LOCATION(PLAYER0,3,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 20)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 30)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 50)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 60)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 70)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 80)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 90)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 100)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 110)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 120)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 130)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 140)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 150)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 160)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 170)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 180)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF
IF(PLAYER0,TIMER0 >= 190)
    ZOOM_TO_LOCATION(PLAYER0,4)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
    ZOOM_TO_LOCATION(PLAYER0,PLAYER0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,10,6)
    USE_POWER_AT_LOCATION(PLAYER0,3,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 220)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,51)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,896,6)
    KILL_CREATURE(PLAYER_GOOD,FAKELORD,ANYWHERE,1)
ENDIF

REM SETTINGS

IF(PLAYER0,TIMER0 >= 45)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe01.ogg")
    QUICK_OBJECTIVE(1,"Here walks the goodly Lord Antonius,'tis he who holds the precious Portal Gem. But only when you've mustered strength sufficient should you challenge him and claim your prize!")
ENDIF

IF(PLAYER0,TIMER0 >= 250)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe02.ogg")
    QUICK_OBJECTIVE(2,"First, there's money to be made. Click on the flashing seam of gold - your Imps will dig out the area you designate.")
ENDIF

IF(PLAYER0,TIMER0 >= 400)
    QUICK_INFORMATION(3,"Welcome to Conquest of Harmonia for keeperfx. Special thanks goes to Qqluqq and Loobinex; they made this possible.")
ENDIF

IF(PLAYER0,TIMER0 >= 800)
    QUICK_OBJECTIVE(4,"Our time is now! The dark forces of the Underworld seek a figurehead, a powerful leader, whose banner they can gather under. To form a force that can, once and for all, end King Reginald- the-Just's sickeningly fair and goodly reign in the Sunlit Kingdom.")
ENDIF

IF(PLAYER0, GOBLIN >= 1)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe05.ogg")
    QUICK_INFORMATION(5,"Look well. A Goblin has entered your domain. On his own he is a cowardly beast, but in a horde becomes a useful fighting force.")

ENDIF

IF(PLAYER0, FIREFLY >= 1)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe06.ogg")
    QUICK_INFORMATION(6,"The swift light Firefly is essential as an airborne scout. His eyes are yours to penetrate dark places and report.")
ENDIF

IF(PLAYER0,LAIR >= 8)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe07.ogg")
    QUICK_OBJECTIVE(7,"Excellent work, Keeper. You are ready to recruit more creatures to your cause. See?  A Portal near your Dungeon Heart, to where your Imps must dig. They'll claim this Portal to be yours: you'll see what beasts emerge.")
ENDIF

IF(PLAYER0,TOTAL_CREATURES >= 1)
    IF(PLAYER0,GARDEN >= 8)
        SET_TIMER(PLAYER0,TIMER1)
        SET_FLAG(PLAYER0,FLAG0,4)
    ENDIF
ENDIF

REM *** BUILT GARDEN ***
IF(PLAYER0,FLAG0 == 4)
    IF(PLAYER0,TOTAL_CREATURES >= 6)
        SET_FLAG(PLAYER0,FLAG0,5)
    ENDIF
ENDIF

REM *** HAS SEVEN OR MORE CREATURES ***
IF(PLAYER0,FLAG0 == 5)
    IF(PLAYER0,TIMER1 >= 600)
        REM CREATE_TEXT(30, 16)
        IF(PLAYER0,TIMER1 >= 800)
            PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe03.ogg")
            QUICK_OBJECTIVE(8,"I sense activity beyond your walls: Dwarfish foes approach, and you may track their path as white dots on your map.")
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON,0,1,300)
            ADD_TUNNELLER_TO_LEVEL(PLAYER_GOOD,1,DUNGEON,0,1,300)
            TUTORIAL_FLASH_BUTTON(37,-1)
            IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
                SET_FLAG(PLAYER0,FLAG0,6)
            ENDIF
        ENDIF
    ENDIF
ENDIF

REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track01.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
    REM Time battle music been playing
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
    REM Time battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
    REM Time no battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
    IF(PLAYER3,FLAG5 != 4)
        NEXT_COMMAND_REUSABLE
        SET_MUSIC("battle01.mp3")
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER3,FLAG5,4)
    ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
    IF(PLAYER3,TIMER5 > 100)
        IF(PLAYER3,FLAG5 != 2)
            NEXT_COMMAND_REUSABLE
            SET_MUSIC("dk2track01.mp3")
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER3,FLAG5,2)
        ENDIF
    ENDIF
ENDIF

IF(PLAYER0,TIMER2 >= 250)
   IF(PLAYER0,IMP <= 4)
        NEXT_COMMAND_REUSABLE
        ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
        NEXT_COMMAND_REUSABLE
        SET_TIMER(PLAYER0,TIMER2)
    ENDIF
ENDIF

REM *** HAS WON FIRST BATTLE ***

IF(PLAYER0,FLAG0 == 6)
    SET_TIMER(PLAYER2,TIMER4)
ENDIF

IF(PLAYER2,TIMER4 >= 50)
        IF(PLAYER0,TOTAL_CREATURES >= 6)
        PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe04.ogg")
        QUICK_OBJECTIVE(9,"Away, Keeper! You shall not taint Smilesville with your evil ways.")
        ADD_PARTY_TO_LEVEL(PLAYER_GOOD,KNIGHT,-1,1)
        ZOOM_TO_LOCATION(PLAYER0,4)
        SET_FLAG(PLAYER0,FLAG0,7)
        ENDIF
ENDIF

IF(PLAYER0,FLAG0 == 7)
    IF(PLAYER_GOOD,TOTAL_CREATURES == 0)
        PLAY_MESSAGE(PLAYER0,SPEECH,"lvl1spe08.ogg")
        QUICK_OBJECTIVE(10,"Bravo, Keeper. Lord Antonius is fallen. The Portal Gem lies aside his helpless form, in wait for Horny to collect it so its yours - and comes he now!")
        ALLY_PLAYERS(PLAYER0,PLAYER5,3)
        ADD_OBJECT_TO_LEVEL(FAKE_GEM,LAST_DEATH_EVENT[PLAYER_GOOD],1,PLAYER5)
        COMPUTER_PLAYER(PLAYER5,0)
        SET_TIMER(PLAYER6,TIMER6)
        WIN_GAME
    ENDIF
ENDIF

HIDE_HERO_GATE(2,1)

IF(PLAYER6,TIMER6 >= 20)
    MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
ENDIF

IF(PLAYER6,TIMER6 >= 60)
    ADD_OBJECT_TO_LEVEL(HORNY_STATUE,-2,1,PLAYER0)
    ZOOM_TO_LOCATION(PLAYER0,-2)
    CREATE_EFFECT(EFFECT_FLASH,PLAYER0)
    CREATE_EFFECT(EFFECT_EXPLOSION_5,-2)
    CREATE_EFFECT(EFFECT_SPANGLE_RED,-2)
    CREATE_EFFECT(EFFECT_WORD_OF_POWER,-2)
ENDIF

IF(PLAYER6,TIMER6 >= 100)
    ADD_PARTY_TO_LEVEL(PLAYER6,HORNY,-2,1)
    SET_OBJECT_CONFIGURATION(HORNY_STATUE,MaximumSize,0)
ENDIF

IF(PLAYER5,HEART_HEALTH > 0)
    SET_FLAG(PLAYER0,FLAG0,8)
ENDIF
IF(PLAYER0,FLAG0 == 8)
    IF(PLAYER5,HEART_HEALTH < 3)
        SET_OBJECT_CONFIGURATION(FAKE_GEM,UpdateFunction,UPDATE_POWER_LIGHTNING)
        SET_TIMER(PLAYER6,TIMER7)
    ENDIF
    IF(PLAYER6,TIMER7 > 50)
        KILL_CREATURE(PLAYER6,EVILLORD,ANYWHERE,1)
    ENDIF
ENDIF