REM ********************************************
REM
REM			 Script for Level 201
REM
REM ********************************************
LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0, 9)
START_MONEY(PLAYER0, 2500)

ADD_CREATURE_TO_POOL(SORCEROR, 4)
ADD_CREATURE_TO_POOL(GOBLIN, 4)
ADD_CREATURE_TO_POOL(FIREFLY, 2)

CREATURE_AVAILABLE(ALL_PLAYERS, SORCEROR, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, GOBLIN, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, FIREFLY, 1, 0)

SET_GAME_RULE(TrainingRoomMaxLevel,4)
SET_GAME_RULE(MaxThingsInHand,30)
SET_GAME_RULE(ImpWorkExperience,124)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)

CREATE_PARTY(DWARVES)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(LORD)
ADD_TO_PARTY(LORD, TUNNELLER	, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(LORD, KNIGHT	, 1, 300, ATTACK_DUNGEON_HEART, 0)

REM START INTRO

SET_HAND_RULE(PLAYER0,ANY_CREATURE,RULE1,DENY,DROPPED_TIME_LOWER,60)
REVEAL_MAP_LOCATION(PLAYER0,PLAYER0,-1)

RUN_AFTER_VICTORY(1)

CREATE_PARTY(HORNY)
    ADD_TO_PARTY(HORNY,EVILLORD,10,0,SNIPE_DUNGEON_HEART,-370)

COMPUTER_PLAYER(PLAYER6,ROAMING)
ALLY_PLAYERS(PLAYER0,PLAYER6,3)
SET_PLAYER_COLOR(PLAYER6,RED)

SET_TIMER(PLAYER0,TIMER0)

IF(PLAYER0,TIMER0 >= 30)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAKELORD,-1,1,1,0)
	SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,0)
	USE_POWER_AT_LOCATION(PLAYER0,4,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 40)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 50)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 60)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 70)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 80)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 90)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 100)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 110)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 120)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 130)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 140)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 150)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 160)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 170)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 180)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 190)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
    ZOOM_TO_LOCATION(PLAYER0,PLAYER0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,10,6)
    USE_POWER_AT_LOCATION(PLAYER0,4,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 220)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,51)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,896,6)
    KILL_CREATURE(PLAYER_GOOD,FAKELORD,ANYWHERE,1)
ENDIF

REM SETTINGS

CONCEAL_MAP_RECT(Player0, 20, 20, 240, 240,1)

REVEAL_MAP_LOCATION(PLAYER0,3,6)

IF(PLAYER0,TIMER0 >= 30)
	QUICK_OBJECTIVE(11,"So forward unto arms against the good Lord Darius, a foe more worthy than feeble Lord Antonius. But battle knowledge gained to claim the Smilesville Portal Gem has put your forces in good stead to build their strength and slay the Hero sickly sweet who now awaits your challenge to his Keep.")
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe04.ogg")
ENDIF

IF(PLAYER0, RESEARCH >= 1)
	QUICK_INFORMATION(12,"The Library. Where Warlocks wise can hone their evil craft perfecting spells for you to cast at goodly foes.")
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe05.ogg")
ENDIF

IF(PLAYER0, SORCEROR >= 1)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe03.ogg")
	QUICK_INFORMATION(13,"Hark now, a Warlock has arrived to study spells within your library. His research will serve your magic well. Restrict him not to his books for within combat he may prove formidable with fireballs.")
ENDIF

IF(PLAYER0, TRAINING >= 1)
	QUICK_INFORMATION(14,"The Training Room is a place where fighting creatures hone their skills.")
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe06.ogg")
ENDIF

IF_AVAILABLE(PLAYER0,POWER_LIGHTNING >= 1)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe09.ogg")
	QUICK_INFORMATION(15,"Your Warlocks now have studied further, offering you another spell to cast, through which a Thunderbolt is yours to hurl against such foolish enemies who dare invade your turf.")
ENDIF

IF_ACTION_POINT(1, PLAYER0)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe01.ogg")
	QUICK_OBJECTIVE(16,"Keeper!  Dare not think that you'll conquer me, you evil wretch. My Dwarves and the power of good will drive you from this land.")
ENDIF

IF_ACTION_POINT(2, PLAYER0)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe02.ogg")
	QUICK_OBJECTIVE(17,"You seek to defile my realm with your foul sorcery? You will perish, Keeper!.")
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DWARVES, -1, 1)
	SET_TIMER(PLAYER_GOOD, TIMER0)
	IF(PLAYER_GOOD, TIMER0 >= 1500)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DWARVES, -1, 1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, LORD, -1, 1)
    		PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe08.ogg")
		QUICK_OBJECTIVE(18,"Another righteous Lord attempts courageous taunts. If words were food, I mark you, these few morsels will be his to choke on soon.  Go to it, Keeper!")
    		ZOOM_TO_LOCATION(PLAYER0,5)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
ENDIF


REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track02.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
	REM Time battle music been playing
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
	REM Time battle has been going on
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
	REM Time no battle has been going on
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
	IF(PLAYER3,FLAG5 != 4)
		NEXT_COMMAND_REUSABLE
		SET_MUSIC("battle01.mp3")
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER3,FLAG5,4)
	ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
	IF(PLAYER3,TIMER5 > 100)
		IF(PLAYER3,FLAG5 != 2)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("dk2track02.mp3")
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER3,FLAG5,2)
		ENDIF
	ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
	IF(PLAYER0,IMP <= 4)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0,TIMER2)
	ENDIF
ENDIF 


IF(PLAYER_GOOD, FLAG0 == 1)
	IF(PLAYER_GOOD, KNIGHT == 0)
		PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe07.ogg")
		QUICK_OBJECTIVE(19,"Lord Darius lies dying. Now comes Horny here to claim for you the Portal Gem. Your progress is most pleasing...")
        ALLY_PLAYERS(PLAYER0,PLAYER5,3)
        ADD_OBJECT_TO_LEVEL(FAKE_GEM,LAST_DEATH_EVENT[PLAYER_GOOD],1,PLAYER5)
        COMPUTER_PLAYER(PLAYER5,0)
        SET_TIMER(PLAYER6,TIMER6)
		WIN_GAME		
	ENDIF
ENDIF

HIDE_HERO_GATE(2,1)

IF(PLAYER6,TIMER6 >= 20)
    MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
ENDIF

IF(PLAYER6,TIMER6 >= 60)
        ADD_OBJECT_TO_LEVEL(HORNY_STATUE,-2,1,PLAYER0)
        ZOOM_TO_LOCATION(PLAYER0,-2)
        CREATE_EFFECT(EFFECT_FLASH,PLAYER0)
        CREATE_EFFECT(EFFECT_EXPLOSION_5,-2)
        CREATE_EFFECT(EFFECT_SPANGLE_RED,-2)
        CREATE_EFFECT(EFFECT_WORD_OF_POWER,-2)
ENDIF

IF(PLAYER6,TIMER6 >= 100)
        ADD_PARTY_TO_LEVEL(PLAYER6,HORNY,-2,1)
        SET_OBJECT_CONFIGURATION(HORNY_STATUE,MaximumSize,0)
ENDIF

IF(PLAYER5,HEART_HEALTH > 0)
    SET_FLAG(PLAYER0,FLAG0,8)
ENDIF
IF(PLAYER0,FLAG0 == 8)
    IF(PLAYER5,HEART_HEALTH < 3)
        SET_OBJECT_CONFIGURATION(FAKE_GEM,UpdateFunction,UPDATE_POWER_LIGHTNING)
        SET_TIMER(PLAYER6,TIMER7)
    ENDIF
    IF(PLAYER6,TIMER7 > 50)
        KILL_CREATURE(PLAYER6,EVILLORD,ANYWHERE,1)
    ENDIF
ENDIF