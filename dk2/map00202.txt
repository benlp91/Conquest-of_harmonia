REM ********************************************
REM
REM             Script for Level 202
REM
REM ********************************************
LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0, 15)
START_MONEY(PLAYER0, 2500)

ADD_CREATURE_TO_POOL(SORCEROR, 5)
ADD_CREATURE_TO_POOL(FIREFLY, 2)
ADD_CREATURE_TO_POOL(GOBLIN, 10)
ADD_CREATURE_TO_POOL(TROLL, 10)


CREATURE_AVAILABLE(ALL_PLAYERS, SORCEROR, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, FIREFLY, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, GOBLIN, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, TROLL, 1, 0)

SET_GAME_RULE(ImpWorkExperience,124)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)
SET_GAME_RULE(MaxThingsInHand,60)

SET_GAME_RULE(TrainingRoomMaxLevel,4)

ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, WORKSHOP, 1, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SIGHT, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, WOOD, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, TURRET, 1, 0)

CREATE_PARTY(DOOR1A)
ADD_TO_PARTY(DOOR1A, DWARFA, 1, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR1B)
ADD_TO_PARTY(DOOR1B, THIEF, 1, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR2)
ADD_TO_PARTY(DOOR2, THIEF, 1, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR2, THIEF, 1, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(LORD)
ADD_TO_PARTY(LORD, THIEF, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(LORD, THIEF, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(LORD, DWARFA, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(LORD, KNIGHT, 2, 300, ATTACK_ENEMIES, 0)

CONCEAL_MAP_RECT(Player0, 25, 25, 240, 240,1)

REVEAL_MAP_LOCATION(PLAYER0,5,6)

DISPLAY_OBJECTIVE(18, PLAYER0)

REM START INTRO

RUN_AFTER_VICTORY(1)

CREATE_PARTY(HORNY)
    ADD_TO_PARTY(HORNY,EVILLORD,10,0,SNIPE_DUNGEON_HEART,-370)

COMPUTER_PLAYER(PLAYER6,ROAMING)
ALLY_PLAYERS(PLAYER0,PLAYER6,3)
SET_PLAYER_COLOR(PLAYER6,RED)

SET_TIMER(PLAYER0,TIMER0)

IF(PLAYER0,TIMER0 >= 30)
    ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAKELORD,-3,1,1,0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,0)
    USE_POWER_AT_LOCATION(PLAYER0,3,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 40)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 50)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 60)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 70)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 80)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 90)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 100)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 110)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 120)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 130)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 140)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 150)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 160)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 170)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 180)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF
IF(PLAYER0,TIMER0 >= 190)
    ZOOM_TO_LOCATION(PLAYER0,6)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
    ZOOM_TO_LOCATION(PLAYER0,PLAYER0)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,10,6)
    USE_POWER_AT_LOCATION(PLAYER0,3,POWER_SIGHT,6,1)
ENDIF

IF(PLAYER0,TIMER0 >= 220)
    SET_POWER_CONFIGURATION(POWER_SIGHT,SoundPlayed,51)
    SET_POWER_CONFIGURATION(POWER_SIGHT,POWER,896,6)
    KILL_CREATURE(PLAYER_GOOD,FAKELORD,ANYWHERE,1)
ENDIF

REM SETTINGS


IF(PLAYER0, TREASURE >= 1)
	DISPLAY_INFORMATION(20, PLAYER0)
ENDIF

IF(PLAYER0, WORKSHOP >= 1)
	DISPLAY_INFORMATION(21, PLAYER0)
ENDIF

IF(PLAYER0, TROLL >= 1)
	DISPLAY_INFORMATION(23, PLAYER0)
ENDIF

IF_AVAILABLE(PLAYER0, WOOD >= 1)
	DISPLAY_INFORMATION(25, PLAYER0)
ENDIF

IF_AVAILABLE(PLAYER0, LIGHTNING >= 1)
	DISPLAY_INFORMATION(26, PLAYER0)
ENDIF

SET_FLAG(PLAYER_GOOD, FLAG2, 0)

PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe01.ogg")

	
SET_TIMER(PLAYER3, TIMER2)

IF(PLAYER3, TIMER2 >= 1020)
PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe02.ogg")
ENDIF

IF(PLAYER0,TROLL ==1)
PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe03.ogg")
ENDIF

IF_ACTION_POINT(4, PLAYER0)
	SET_FLAG(PLAYER_GOOD, FLAG0, 0)
	SET_FLAG(PLAYER_GOOD, FLAG1, 0)
	SET_TIMER(PLAYER0, TIMER2)
	SET_TIMER(PLAYER0, TIMER1)
ENDIF

IF(PLAYER_GOOD, FLAG0 == 0)
	IF(PLAYER0, TIMER2 >= 1020)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1A, -1, 1)
		
		IF(PLAYER0, TIMER2 >= 2160)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1B, -1, 1)
		ENDIF
		
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0, TIMER2)
	ENDIF
ENDIF

IF_ACTION_POINT(1, PLAYER0)
	SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	DISPLAY_INFORMATION(19, PLAYER0)
ENDIF

IF(PLAYER_GOOD, FLAG1 == 0)
	IF(PLAYER0, TIMER1 >= 1440)
		NEXT_COMMAND_REUSABLE
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR2, -2, 1)
		
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0, TIMER1)
	ENDIF
ENDIF

IF_ACTION_POINT(2, PLAYER0)
	SET_FLAG(PLAYER_GOOD, FLAG1, 1)
	DISPLAY_INFORMATION(19, PLAYER0)
ENDIF

IF(PLAYER0,TOTAL_GOLD_MINED >= 90000)
		SET_FLAG(PLAYER_GOOD, FLAG2, 1)
		DISPLAY_OBJECTIVE(22, PLAYER0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, LORD, -3, 1)
		PLAY_MESSAGE(PLAYER0,SPEECH,"lvl3spe04.ogg")
ENDIF


REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track01.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
    REM Time battle music been playing
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
    REM Time battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
    REM Time no battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
    IF(PLAYER3,FLAG5 != 4)
        NEXT_COMMAND_REUSABLE
        SET_MUSIC("battle01.mp3")
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER3,FLAG5,4)
    ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
    IF(PLAYER3,TIMER5 > 100)
        IF(PLAYER3,FLAG5 != 2)
            NEXT_COMMAND_REUSABLE
            SET_MUSIC("dk2track01.mp3")
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER3,FLAG5,2)
        ENDIF
    ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
   		IF(PLAYER0,IMP <= 4)
        		NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
        		NEXT_COMMAND_REUSABLE
        		SET_TIMER(PLAYER0,TIMER2)
		ENDIF
ENDIF 


IF(PLAYER_GOOD, FLAG2 == 1)
	IF(PLAYER_GOOD, KNIGHT == 0)
		DISPLAY_OBJECTIVE(24, PLAYER0)

        ALLY_PLAYERS(PLAYER0,PLAYER5,3)
        ADD_OBJECT_TO_LEVEL(FAKE_GEM,LAST_DEATH_EVENT[PLAYER_GOOD],1,PLAYER5)
        COMPUTER_PLAYER(PLAYER5,0)
        SET_TIMER(PLAYER6,TIMER6)
		WIN_GAME		
	ENDIF
ENDIF

HIDE_HERO_GATE(4,1)

IF(PLAYER6,TIMER6 >= 20)
    MAGIC_AVAILABLE(PLAYER0,POWER_POSSESS,0,0)
ENDIF

IF(PLAYER6,TIMER6 >= 60)
        PLAY_MESSAGE(PLAYER0,SOUND,1)
        ADD_OBJECT_TO_LEVEL(HORNY_STATUE,-4,1,PLAYER0)
        ZOOM_TO_LOCATION(PLAYER0,-4)
        CREATE_EFFECT(EFFECT_FLASH,PLAYER0)
        CREATE_EFFECT(EFFECT_EXPLOSION_5,-4)
        CREATE_EFFECT(EFFECT_SPANGLE_RED,-4)
        CREATE_EFFECT(EFFECT_WORD_OF_POWER,-4)
ENDIF

IF(PLAYER6,TIMER6 >= 100)
        ADD_PARTY_TO_LEVEL(PLAYER6,HORNY,-4,1)
        SET_OBJECT_CONFIGURATION(HORNY_STATUE,MaximumSize,0)
ENDIF

IF(PLAYER5,HEART_HEALTH > 0)
    SET_FLAG(PLAYER0,FLAG0,8)
ENDIF
IF(PLAYER0,FLAG0 == 8)
    IF(PLAYER5,HEART_HEALTH < 3)
        SET_OBJECT_CONFIGURATION(FAKE_GEM,UpdateFunction,UPDATE_POWER_LIGHTNING)
        SET_TIMER(PLAYER6,TIMER7)
    ENDIF
    IF(PLAYER6,TIMER7 > 50)
        KILL_CREATURE(PLAYER6,EVILLORD,ANYWHERE,1)
    ENDIF
ENDIF