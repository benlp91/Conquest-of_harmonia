REM ********************************************
REM
REM             Script for Level 220
REM
REM ********************************************

LEVEL_VERSION(1)

SET_GENERATE_SPEED(400)
BONUS_LEVEL_TIME(8000)

START_MONEY(ALL_PLAYERS,60000)

COMPUTER_PLAYER(PLAYER1,ROAMING)
COMPUTER_PLAYER(PLAYER2,ROAMING)
COMPUTER_PLAYER(PLAYER3,ROAMING)


MAGIC_AVAILABLE(ALL_PLAYERS,POWER_HAND,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_SLAP,1,1)
MAGIC_AVAILABLE(ALL_PLAYERS,POWER_LIGHTNING,1,1)

CONCEAL_MAP_RECT(Player0, 70, 70, 250, 250,1)

IF(PLAYER0, TIMER0 >= 55)
QUICK_INFORMATION(123,"Turn on the low walls mode in your options to play this level!")
ENDIF

RUN_AFTER_VICTORY(1)

IF(PLAYER0, TIMER0 >= 20)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl21spe01.ogg")
	QUICK_OBJECTIVE(121,"Even malignant overlords need a little rest and recuperation from time to time Keeper. Use the boulders to negotiate your way through these three courses. Be warned though. You have a limited amount of slaps to remain under par, and an even more limited amount of time.")
	REVEAL_MAP_LOCATION(PLAYER0,4,50)
	REVEAL_MAP_LOCATION(PLAYER0,6,50)
ENDIF

IF(PLAYER0, TIMER0 >= 35)
	ZOOM_TO_LOCATION(PLAYER0,10)
ENDIF

IF(PLAYER0, TIMER0 >= 85)
	ZOOM_TO_LOCATION(PLAYER0,1)
ENDIF

IF(PLAYER1,DUNGEON_DESTROYED ==1)
	SET_TIMER(PLAYER0, TIMER1)
	CHANGE_SLAB_TYPE(4,26,LAVA,MATCH)
ENDIF

IF(PLAYER0, TIMER1 >= 30)
	ZOOM_TO_LOCATION(PLAYER0,2)
	REVEAL_MAP_LOCATION(PLAYER0,5,80)
	REVEAL_MAP_LOCATION(PLAYER0,7,80)
ENDIF

IF(PLAYER2,DUNGEON_DESTROYED ==1)
	SET_TIMER(PLAYER0, TIMER2)
	CHANGE_SLAB_TYPE(33,3,LAVA,MATCH)
ENDIF

IF(PLAYER0, TIMER2 >= 30)
	ZOOM_TO_LOCATION(PLAYER0,3)
	REVEAL_MAP_LOCATION(PLAYER0,9,200)
ENDIF

IF(PLAYER3,DUNGEON_DESTROYED ==1)
	WIN_GAME
	CHANGE_SLAB_TYPE(25,41,LAVA,MATCH)
	PLAY_MESSAGE(PLAYER0,SPEECH,"lvl21spe02.ogg")
	QUICK_OBJECTIVE(122,"Well done Keeper, you have completed your task.")
	ZOOM_TO_LOCATION(PLAYER0,11)
	ADD_CREATURE_TO_LEVEL(PLAYER0,DARK_MISTRESS,11,1,5,300)
	ADD_CREATURE_TO_LEVEL(PLAYER0,TROLL,11,1,5,300)
	ADD_CREATURE_TO_LEVEL(PLAYER0,SORCEROR,11,1,5,300)
ENDIF

SET_TIMER(PLAYER0, TIMER0)

IF(PLAYER0, TIMER0 >= 8000)
	LOSE_GAME
ENDIF