REM ********************************************
REM
REM             Script for Level 203
REM
REM ********************************************
LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0, 15)
START_MONEY(PLAYER0, 2500)

REM For Testing Only ----------
REM REVEAL_MAP_RECT(PLAYER0,127,127,254,254)

REM -------------

ADD_CREATURE_TO_POOL(SORCEROR, 5)
ADD_CREATURE_TO_POOL(TROLL, 10)
ADD_CREATURE_TO_POOL(FIREFLY, 1)
ADD_CREATURE_TO_POOL(GOBLIN, 10)
ADD_CREATURE_TO_POOL(DARK_ELF,20)

CREATURE_AVAILABLE(ALL_PLAYERS, SORCEROR, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, FIREFLY, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, TROLL, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, GOBLIN, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, DARK_ELF,1,0)

SET_GAME_RULE(TrainingRoomMaxLevel,4)

ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TREASURE, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, WORKSHOP, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GUARD_POST, 3, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)

DOOR_AVAILABLE(ALL_PLAYERS, WOOD, 1, 0)
TRAP_AVAILABLE(ALL_PLAYERS, LIGHTNING, 1, 0)

SET_GAME_RULE(ImpWorkExperience,124)

SET_GAME_RULE(MaxThingsInHand,60)

CONCEAL_MAP_RECT(Player0, 5, 5, 440, 440,1)

REVEAL_MAP_LOCATION(PLAYER0,5,6)

CREATE_PARTY(DOOR1A)
ADD_TO_PARTY(DOOR1A, DWARFA, 1, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR1A, DWARFA, 2, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR1B)
ADD_TO_PARTY(DOOR1B, THIEF, 1, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR1B, THIEF, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR1B, BARBARIAN, 1, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR2A)
ADD_TO_PARTY(DOOR2A, DWARFA, 1, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR2A, DWARFA, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR2A, THIEF, 2, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR2B)
ADD_TO_PARTY(DOOR2B, BARBARIAN, 1, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR2B, DWARFA, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR2B, THIEF, 2, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR3A)
ADD_TO_PARTY(DOOR3A, DWARFA, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR3A, THIEF, 2, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR4A)
ADD_TO_PARTY(DOOR4A, DWARFA, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR4A, BARBARIAN, 2, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR4B)
ADD_TO_PARTY(DOOR4B, BARBARIAN, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR4B, BARBARIAN, 2, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR4B, THIEF, 1, 300, ATTACK_ENEMIES, 0)

CREATE_PARTY(DOOR5A)
ADD_TO_PARTY(DOOR5A, THIEF, 3, 300, ATTACK_ENEMIES, 0)
ADD_TO_PARTY(DOOR5A, THIEF, 2, 300, ATTACK_ENEMIES, 0)

if(player0,ENTRANCE == 9)
  next_command_reusable
  max_creatures(Player0,15)
ENDIF
if(player0,ENTRANCE == 18)
  next_command_reusable
  max_creatures(Player0,20)
ENDIF


DISPLAY_OBJECTIVE(27,PLAYER0)

IF (PLAYER0, GUARD_POST >= 1)
	DISPLAY_INFORMATION(28,PLAYER0)
ENDIF

IF (PLAYER0, DARK_ELF >= 1)
	DISPLAY_INFORMATION(29,PLAYER0)
ENDIF

IF_ACTION_POINT(4, PLAYER0)
	SET_TIMER(PLAYER0, TIMER0)
	IF(PLAYER0, TIMER0 >= 2160)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1A, -5, 1)
		IF(PLAYER0, TIMER0 >= 3000)
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR2A, -6, 1)
			IF(PLAYER0, TIMER0 >= 4500)
				ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1B, -5, 1)
				IF(PLAYER0, TIMER0 >= 5750)
					ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR2B, -6, 1)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF_ACTION_POINT(1, PLAYER0)
	SET_TIMER(PLAYER0, TIMER1)
ENDIF

IF(PLAYER0, TIMER1 >= 1450)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR4A, -1, 1)
ENDIF
IF(PLAYER0, TIMER1 >= 2350)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR4B, -1, 1)
ENDIF


IF_ACTION_POINT(2, PLAYER0)
	SET_TIMER(PLAYER0, TIMER2)
	IF(PLAYER0, TIMER2 >= 750)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR3A, -2, 1)
		
	ENDIF
ENDIF

IF_ACTION_POINT(3, PLAYER0)
	SET_FLAG(PLAYER_GOOD, FLAG2, 1)
	DISPLAY_OBJECTIVE(30,PLAYER0)
	SET_TIMER(PLAYER0, TIMER3)
	IF(PLAYER_GOOD, FLAG2 == 1)
		IF(PLAYER0, TIMER3 >= 150)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1A, -3, 1)
			NEXT_COMMAND_REUSABLE
			ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR5A, -4, 1)
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER_GOOD, FLAG2, 0)
		ENDIF
		IF(PLAYER0, TIMER3 >= 300)
			NEXT_COMMAND_REUSABLE	
			SET_TIMER(PLAYER0, TIMER3)
		ENDIF
	ENDIF
ENDIF

REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track02.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
    REM Time battle music been playing
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
    REM Time battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
    REM Time no battle has been going on
    NEXT_COMMAND_REUSABLE
    SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
    IF(PLAYER3,FLAG5 != 4)
        NEXT_COMMAND_REUSABLE
        SET_MUSIC("battle02.mp3")
        NEXT_COMMAND_REUSABLE
        SET_FLAG(PLAYER3,FLAG5,4)
    ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
    IF(PLAYER3,TIMER5 > 100)
        IF(PLAYER3,FLAG5 != 2)
            NEXT_COMMAND_REUSABLE
            SET_MUSIC("dk2track02.mp3")
            NEXT_COMMAND_REUSABLE
            SET_FLAG(PLAYER3,FLAG5,2)
        ENDIF
    ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
   		IF(PLAYER0,IMP <= 4)
        		NEXT_COMMAND_REUSABLE
			ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
        		NEXT_COMMAND_REUSABLE
        		SET_TIMER(PLAYER0,TIMER2)
		ENDIF
ENDIF 

IF_ACTION_POINT(3, PLAYER0)
    IF(PLAYER_GOOD, KNIGHT == 0)
	SET_FLAG(PLAYER_GOOD, FLAG2, 2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1A, -3, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR1B, -3, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR2A, -4, 1)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DOOR2B, -4, 1)



	    		DISPLAY_OBJECTIVE(31,PLAYER0)
			ROOM_AVAILABLE(PLAYER0, BRIDGE,1,1)
			WIN_GAME

  ENDIF
ENDIF


