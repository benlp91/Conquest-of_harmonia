REM ********************************************
REM
REM			 Script for Level 201
REM
REM ********************************************
LEVEL_VERSION(1)
SET_GENERATE_SPEED(400)

MAX_CREATURES(PLAYER0, 9)
START_MONEY(PLAYER0, 2500)

ADD_CREATURE_TO_POOL(SORCEROR, 4)
ADD_CREATURE_TO_POOL(GOBLIN, 4)
ADD_CREATURE_TO_POOL(FIREFLY, 2)

CREATURE_AVAILABLE(ALL_PLAYERS, SORCEROR, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, GOBLIN, 1, 0)
CREATURE_AVAILABLE(ALL_PLAYERS, FIREFLY, 1, 0)

SET_GAME_RULE(TrainingRoomMaxLevel,4)
SET_GAME_RULE(MaxThingsInHand,60)
SET_GAME_RULE(ImpWorkExperience,124)
SET_CREATURE_PROPERTY(KNIGHT,LORD,0)

ROOM_AVAILABLE(ALL_PLAYERS, RESEARCH, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, GARDEN, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, LAIR, 1, 1)
ROOM_AVAILABLE(ALL_PLAYERS, TRAINING, 1, 1)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_OBEY, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_IMP, 1, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_LIGHTNING, 1, 0)

CREATE_PARTY(DWARVES)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(DWARVES, DWARFA, 1, 300, ATTACK_DUNGEON_HEART, 0)

CREATE_PARTY(LORD)
ADD_TO_PARTY(LORD, TUNNELLER	, 1, 300, ATTACK_DUNGEON_HEART, 0)
ADD_TO_PARTY(LORD, KNIGHT	, 1, 300, ATTACK_DUNGEON_HEART, 0)


REM START INTRO
SET_TIMER(PLAYER0,TIMER0)
USE_POWER_AT_LOCATION(PLAYER0,4,POWER_SIGHT,6,1)
ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,FAKELORD,4,1,1,0)

IF(PLAYER0,TIMER0 >= 40)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 60)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 80)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 100)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 120)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 140)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 160)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF
IF(PLAYER0,TIMER0 >= 180)
    ZOOM_TO_LOCATION(PLAYER0,5)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
    ZOOM_TO_LOCATION(PLAYER0,PLAYER0)
    USE_POWER_AT_LOCATION(PLAYER0,6,POWER_SIGHT,1,1)
ENDIF

IF(PLAYER0,TIMER0 >= 220)
    KILL_CREATURE(PLAYER_GOOD,FAKELORD,ANYWHERE,1)
ENDIF

REM SETTINGS












CONCEAL_MAP_RECT(Player0, 20, 20, 240, 240,1)

REVEAL_MAP_LOCATION(PLAYER0,3,6)

DISPLAY_OBJECTIVE(10,PLAYER0)

IF(PLAYER0, RESEARCH >= 1)
	DISPLAY_OBJECTIVE(11,PLAYER0)
ENDIF

IF(PLAYER0, SORCEROR >= 1)
	DISPLAY_OBJECTIVE(12,PLAYER0)
ENDIF

IF(PLAYER0, TRAINING >= 1)
	DISPLAY_INFORMATION(13,PLAYER0)
ENDIF

IF_AVAILABLE(PLAYER0,POWER_LIGHTNING >= 1)
	DISPLAY_INFORMATION(14,PLAYER0)
ENDIF

PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe00.ogg")

IF_ACTION_POINT(1, PLAYER0)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe01.ogg")
ENDIF

IF_ACTION_POINT(2, PLAYER0)
    PLAY_MESSAGE(PLAYER0,SPEECH,"lvl2spe02.ogg")
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DWARVES, -1, 1)
	SET_TIMER(PLAYER_GOOD, TIMER0)
	IF(PLAYER_GOOD, TIMER0 >= 1500)
		DISPLAY_OBJECTIVE(16,PLAYER0)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, DWARVES, -1, 1)
		ADD_PARTY_TO_LEVEL(PLAYER_GOOD, LORD, -1, 1)
		SET_FLAG(PLAYER_GOOD, FLAG0, 1)
	ENDIF
ENDIF


REM PL3,FLAG5 active music track
rem value 2: dk2track02.mp3
rem value 4: dk2track04.mp3
SET_MUSIC("dk2track02.mp3")
SET_FLAG(PLAYER3,FLAG5,2)

IF(PLAYER3,FLAG5 != 4)
	REM Time battle music been playing
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER7)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES == 0)
	REM Time battle has been going on
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER6)
ENDIF
IF(PLAYER0,ACTIVE_BATTLES > 0)
	REM Time no battle has been going on
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER3,TIMER5)
ENDIF

REM Start battle music if battle has been going on for at least 60 gameturns (3 seconds) and is not already playing
IF(PLAYER3,TIMER6 > 60)
	IF(PLAYER3,FLAG5 != 4)
		NEXT_COMMAND_REUSABLE
		SET_MUSIC("battle01.mp3")
		NEXT_COMMAND_REUSABLE
		SET_FLAG(PLAYER3,FLAG5,4)
	ENDIF
ENDIF

REM Start regular music if battle music lasted at least 15 seconds and no fighting has occured for at least 5 seconds
IF(PLAYER3,TIMER7 > 300)
	IF(PLAYER3,TIMER5 > 100)
		IF(PLAYER3,FLAG5 != 2)
			NEXT_COMMAND_REUSABLE
			SET_MUSIC("dk2track02.mp3")
			NEXT_COMMAND_REUSABLE
			SET_FLAG(PLAYER3,FLAG5,2)
		ENDIF
	ENDIF
ENDIF

SET_TIMER(PLAYER0,TIMER2)

IF(PLAYER0,TIMER2 >= 250)
	IF(PLAYER0,IMP <= 4)
		NEXT_COMMAND_REUSABLE
		ADD_CREATURE_TO_LEVEL(PLAYER0,IMP,PLAYER0,2,1,100)
		NEXT_COMMAND_REUSABLE
		SET_TIMER(PLAYER0,TIMER2)
	ENDIF
ENDIF 


IF(PLAYER_GOOD, FLAG0 == 1)
	IF(PLAYER_GOOD, KNIGHT == 0)
		DISPLAY_OBJECTIVE(17,PLAYER0)
		WIN_GAME
	ENDIF
ENDIF
