REM ********************************************
REM
REM             Script for Level EASTER EGG
REM
REM ********************************************

LEVEL_VERSION(1)

SET_GENERATE_SPEED(400)

START_MONEY(ALL_PLAYERS, 2000)

MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HAND, 0, 0)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_SLAP, 1, 1)
MAGIC_AVAILABLE(ALL_PLAYERS, POWER_HEAL_CREATURE, 1, 1)

CREATE_PARTY(DOOR1)
ADD_TO_PARTY(DOOR1, FAKELORD, 10, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(DOOR2A)
ADD_TO_PARTY(DOOR2A, FAIRY, 2, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(DOOR2A, WITCH, 2, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(DOOR2B)
ADD_TO_PARTY(DOOR2B, WITCH, 2, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(DOOR2B, FAIRY, 2, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(DOOR2C)
ADD_TO_PARTY(DOOR2C, FAIRY, 2, 0, DEFEND_ROOMS, 0)
ADD_TO_PARTY(DOOR2C, FAIRY, 2, 0, DEFEND_ROOMS, 0)

CREATE_PARTY(DOOR2D)
ADD_TO_PARTY(DOOR2D, WITCH, 1, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(DOOR2D, WITCH, 1, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(DOOR2D, FAIRY, 2, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(batl1)
ADD_TO_PARTY(batl1, FAIRY, 1, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(batl)
ADD_TO_PARTY(batl, FAIRY, 1, 300, DEFEND_LOCATION, 0)

CREATE_PARTY(batl3)
ADD_TO_PARTY(batl3, WITCH, 1, 300, DEFEND_LOCATION, 0)

CREATE_PARTY(batl2)
ADD_TO_PARTY(batl2, WITCH, 1, 300, DEFEND_ROOMS, 0)

CREATE_PARTY(batl4)
ADD_TO_PARTY(batl4, HELL_HOUND, 1, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(batl4, HELL_HOUND, 1, 300, DEFEND_ROOMS, 0)
ADD_TO_PARTY(batl4, HELL_HOUND, 1, 300, DEFEND_ROOMS, 0)

SET_TIMER(PLAYER0,TIMER0)
SET_TIMER(PLAYER0,TIMER1)
SET_TIMER(PLAYER0,TIMER2)
SET_TIMER(PLAYER0,TIMER3)
SET_TIMER(PLAYER0,TIMER4)

SET_GAME_RULE(FriendlyFightAreaDamagePercent,0)
SET_GAME_RULE(FriendlyFightAreaRangePercent,0)
SET_OBJECT_CONFIGURATION(SOUL_CONTAINER,MaximumSize,0)

RUN_AFTER_VICTORY(1)

REVEAL_MAP_LOCATION(PLAYER0,1,40)

SET_GAME_RULE(BodyRemainsFor,100)

SET_PLAYER_COLOR(PLAYER_GOOD,RED)

SET_CREATURE_CONFIGURATION(SORCEROR,BaseSpeed,0)
SET_CREATURE_CONFIGURATION(SORCEROR,FieldOfView,0)
SET_CREATURE_CONFIGURATION(SORCEROR,DropDead,0)
SET_CREATURE_CONFIGURATION(SORCEROR,Scream,0)
SET_CREATURE_CONFIGURATION(SORCEROR,DeadSplat,981)

SET_OBJECT_CONFIGURATION(SORCEROR_STUNNED,MaximumSize,400)

IF(PLAYER0,TIMER0 >= 45)
	ZOOM_TO_LOCATION(PLAYER0,1)
ENDIF

IF(PLAYER0,TIMER0 >= 20)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_CHICKEN,MaximumSize,0)
ENDIF

IF(PLAYER0,TIMER0 >= 65)
	PLAY_MESSAGE(PLAYER0,SPEECH,"easter.ogg")
	QUICK_MESSAGE(80,"You will regret your foolish Challenge.",AVATAR)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,600)
	CREATE_EFFECT(13,5)
ENDIF

IF(PLAYER0,TIMER0 >= 75)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,600)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_STUNNED,MaximumSize,0)
	ADD_CREATURE_TO_LEVEL(PLAYER0,SORCEROR,4,1,10,0)
ENDIF

IF(PLAYER0,TIMER0 >= 85)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,600)
	PLACE_TRAP(PLAYER_GOOD,FREEZE_TRAP_BONUS,76,12,1)
ENDIF

IF(PLAYER0,TIMER0 >= 70)
	CREATE_EFFECT(13,5)
ENDIF

IF(PLAYER0,TIMER0 >= 125)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,600)
	SET_OBJECT_CONFIGURATION(SORCEROR_CHICKEN,MaximumSize,0)
ENDIF

IF(PLAYER0,TIMER0 >= 135)
	PLACE_TRAP(PLAYER_GOOD,LIGHTNINGTRAP_BONUS,76,13,1)
	CREATE_EFFECT(47,4)
	CREATE_EFFECT(48,4)
ENDIF

IF(PLAYER0,TIMER0 >= 145)
	CREATE_EFFECT(47,4)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,400)
	KILL_CREATURE(PLAYER0,SORCEROR,ANYWHERE,1)
ENDIF

IF(PLAYER0,TIMER0 >= 155)
	CREATE_EFFECT(47,4)
ENDIF

IF(PLAYER0,TIMER0 >= 165)
	CREATE_EFFECT(47,4)
ENDIF

IF(PLAYER0,TIMER0 >= 160)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,400)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,600)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,0)
	CREATE_EFFECT(14,6)
	PLAY_MESSAGE(PLAYER0,SPEECH,"laugh.ogg")

ENDIF

IF(PLAYER0,TIMER0 >= 170)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,375)
ENDIF

IF(PLAYER0,TIMER0 >= 175)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,350)
ENDIF

IF(PLAYER0,TIMER0 >= 180)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,325)
ENDIF

IF(PLAYER0,TIMER0 >= 185)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,300)
ENDIF

IF(PLAYER0,TIMER0 >= 190)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,250)
ENDIF

IF(PLAYER0,TIMER0 >= 195)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,200)
ENDIF

IF(PLAYER0,TIMER0 >= 200)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,175)
ENDIF

IF(PLAYER0,TIMER0 >= 210)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,150)
ENDIF

IF(PLAYER0,TIMER0 >= 220)
	SET_OBJECT_CONFIGURATION(SORCEROR_CHICKEN,MaximumSize,400)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,0)
ENDIF


IF(PLAYER0,TIMER0 >= 220)
	SET_OBJECT_CONFIGURATION(FEATHER_EFFECT,MaximumSize,500)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,600)
ENDIF


IF(PLAYER0,TIMER0 >= 240)
	CREATE_EFFECT(13,5)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_SPELLED,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(SORCEROR_CHICKEN,MaximumSize,0)
	ADD_CREATURE_TO_LEVEL(PLAYER0,EASTER_CHICKEN,4,1,1,0)
	SET_OBJECT_CONFIGURATION(LAIR_SORCR,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(EASTER_CHICKEN_LAIR,MaximumSize,300)
ENDIF

IF(PLAYER0,TIMER0 >= 250)
	SET_OBJECT_CONFIGURATION(FEATHER_EFFECT,MaximumSize,0)
ENDIF

IF(PLAYER0,TIMER0 >= 260)
	USE_POWER_ON_CREATURE(PLAYER0,EASTER_CHICKEN,ANYWHERE,PLAYER0,POWER_POSSESS,1,1)
REM	LOCK_POSSESSION(PLAYER0,1)
	PLAY_MESSAGE(PLAYER0,SOUND,159)
ENDIF

REM AVATAR APPEARS THE SECOND TIME

IF_ACTION_POINT(7,PLAYER0)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,400)
	CREATE_EFFECT(13,8)
	SET_TIMER(PLAYER_NEUTRAL,TIMER3)
ENDIF

IF(PLAYER_NEUTRAL,TIMER3 >=30)
	PLAY_MESSAGE(PLAYER0,SPEECH,"laugh.ogg")
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DOOR2C,-1,2)
	ADD_PARTY_TO_LEVEL(PLAYER_GOOD,DOOR2C,-2,2)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,400)
	SET_FLAG(PLAYER5,FLAG0,1)
ENDIF

IF(PLAYER_NEUTRAL,TIMER3 >=80)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,400)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
ENDIF

IF(PLAYER_NEUTRAL,TIMER3 >=100)
	CREATE_EFFECT(13,8)
	SET_OBJECT_CONFIGURATION(LORD_ATTACK,MaximumSize,0)
ENDIF

IF(PLAYER5,FLAG0==1)
	IF(PLAYER_GOOD,FAIRY == 0)
		QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
		REM CHICKEN LVL 2
		USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
		PLAY_MESSAGE(PLAYER0,SOUND,76)
		SET_TIMER(PLAYER5,TIMER5)
	ENDIF
ENDIF

IF(PLAYER5,TIMER5 >= 20)
		CHANGE_SLAB_TYPE(24,16,PRETTY_PATH)
		CHANGE_SLAB_TYPE(25,16,PRETTY_PATH)
		CHANGE_SLAB_TYPE(26,16,PRETTY_PATH)
		PLAY_MESSAGE(PLAYER0,SOUND,77)
ENDIF

IF(PLAYER5,TIMER5 >= 30)
		CHANGE_SLAB_TYPE(25,15,PRETTY_PATH)
		PLAY_MESSAGE(PLAYER0,SOUND,77)
ENDIF

IF(PLAYER5,TIMER5 >= 40)
		CHANGE_SLAB_TYPE(25,14,PRETTY_PATH)
		PLAY_MESSAGE(PLAYER0,SOUND,77)
ENDIF

IF_ACTION_POINT(2,PLAYER0)
		REM chicken lvl 3
		QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
		USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
		PLAY_MESSAGE(PLAYER0,SOUND,76)
ENDIF

REM end sequence 2
		SET_OBJECT_CONFIGURATION(SPECBOX_CUSTOM_A,MaximumSize,200)

IF_ACTION_POINT(10,PLAYER0)
		REM chicken lvl 4
		QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
		USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
		PLAY_MESSAGE(PLAYER0,SOUND,76)
		SET_OBJECT_CONFIGURATION(SPECBOX_CUSTOM_A,MaximumSize,0)
		SET_TIMER(PLAYER5,TIMER2)
ENDIF

IF(PLAYER5,TIMER2 >= 10)
		REM chicken lvl 5
		USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
ENDIF

REM send the inforcements

IF_ACTION_POINT(11,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,12,5,5,200,FALL)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,13,5,5,200,FALL)
ENDIF

REM healing

SET_TIMER(PLAYER2,TIMER2)

IF(PLAYER2,TIMER2 >= 5)
	NEXT_COMMAND_REUSABLE
	ADD_HEART_HEALTH(PLAYER0,10000,0)
	NEXT_COMMAND_REUSABLE
	COUNT_CREATURES_AT_ACTION_POINT(3,PLAYER0,ANY_CREATURE,PLAYER0,FLAG0)
ENDIF

IF(PLAYER2,TIMER2 >= 10)
	NEXT_COMMAND_REUSABLE
	COUNT_CREATURES_AT_ACTION_POINT(15,PLAYER0,ANY_CREATURE,PLAYER0,FLAG0)
ENDIF

IF(PLAYER2,TIMER2 >= 15)
	NEXT_COMMAND_REUSABLE
	COUNT_CREATURES_AT_ACTION_POINT(22,PLAYER0,ANY_CREATURE,PLAYER0,FLAG0)
ENDIF

IF(PLAYER2,TIMER2 >= 20)
	NEXT_COMMAND_REUSABLE
	COUNT_CREATURES_AT_ACTION_POINT(2,PLAYER0,ANY_CREATURE,PLAYER0,FLAG0)
	NEXT_COMMAND_REUSABLE
	SET_TIMER(PLAYER2,TIMER2)
ENDIF

IF(PLAYER0,FLAG0 >0)
	NEXT_COMMAND_REUSABLE
	USE_POWER_ON_CREATURE(PLAYER0,ANY_CREATURE,ON_FRIENDLY_GROUND,PLAYER0,POWER_HEAL_CREATURE,1,1)
	NEXT_COMMAND_REUSABLE
	SET_FLAG(PLAYER0,FLAG0,0)
ENDIF

IF_ACTION_POINT(9,PLAYER0)
	REM chicken is lvl 6 now
	QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
	USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,DWARFA,-3,3,3,0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,KNIGHT,-3,3,6,0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,ARCHER,-3,3,4,0)
ENDIF

IF_ACTION_POINT(14,PLAYER0)
	REM CHICKEN LVL 7
	QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
	USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
ENDIF


IF_ACTION_POINT(15,PLAYER0)
	REM CHICKEN LVL 8
	QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
	USE_SPECIAL_INCREASE_LEVEL(PLAYER0,1)
ENDIF


IF_ACTION_POINT(18,PLAYER0)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,GIANT,19,7,10,0,FALL)
	ADD_CREATURE_TO_LEVEL(PLAYER_GOOD,BARBARIAN,19,7,10,0,FALL)
ENDIF


REM AVATAR THIRD TIME

IF_ACTION_POINT(16,PLAYER0)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,400)
	CREATE_EFFECT(13,17)
	CHANGE_SLAB_TYPE(29,43,LAVA)
	CHANGE_SLAB_TYPE(16,43,LAVA)
	SET_TIMER(PLAYER0,TIMER7)
ENDIF

IF(PLAYER0,TIMER7 >= 20)
	SET_OBJECT_CONFIGURATION(LORD_STATUE,MaximumSize,0)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,400)
	CHANGE_SLAB_TYPE(17,40,LAVA)
	CHANGE_SLAB_TYPE(18,40,LAVA)
	CHANGE_SLAB_TYPE(19,40,LAVA)
	CHANGE_SLAB_TYPE(20,40,LAVA)
	CHANGE_SLAB_TYPE(21,40,LAVA)
	CHANGE_SLAB_TYPE(22,40,LAVA)	
	CHANGE_SLAB_TYPE(23,40,LAVA)
	CHANGE_SLAB_TYPE(24,40,LAVA)
	CHANGE_SLAB_TYPE(25,40,LAVA)
	CHANGE_SLAB_TYPE(26,40,LAVA)	
	CHANGE_SLAB_TYPE(27,40,LAVA)
	CHANGE_SLAB_TYPE(28,40,LAVA)

	CHANGE_SLAB_TYPE(17,46,LAVA)
	CHANGE_SLAB_TYPE(18,46,LAVA)
	CHANGE_SLAB_TYPE(19,46,LAVA)
	CHANGE_SLAB_TYPE(20,46,LAVA)
	CHANGE_SLAB_TYPE(21,46,LAVA)
	CHANGE_SLAB_TYPE(22,46,LAVA)	
	CHANGE_SLAB_TYPE(23,46,LAVA)
	CHANGE_SLAB_TYPE(24,46,LAVA)
	CHANGE_SLAB_TYPE(25,46,LAVA)
	CHANGE_SLAB_TYPE(26,46,LAVA)	
	CHANGE_SLAB_TYPE(27,46,LAVA)
	CHANGE_SLAB_TYPE(28,46,LAVA)
	PLAY_MESSAGE(PLAYER0,SPEECH,"laugh.ogg")
	PLAY_MESSAGE(PLAYER0,SOUND,77)
ENDIF

IF(PLAYER0,TIMER7 >= 80)
	SET_OBJECT_CONFIGURATION(LORD_LAUGH,MaximumSize,0)
ENDIF

IF(PLAYER0,TIMER7 >= 300)
	CHANGE_SLAB_TYPE(16,43,PRETTY_PATH)
ENDIF

IF_ACTION_POINT(20,PLAYER0)
	QUICK_MESSAGE(100,"new Spell learned.",PLAYER0)
	USE_SPECIAL_INCREASE_LEVEL(PLAYER0,2)
	SET_TIMER(PLAYER1,TIMER7)
ENDIF

IF(PLAYER1,TIMER7 >= 20)
	CHANGE_SLAB_TYPE(15,43,LAVA)
	PLAY_MESSAGE(PLAYER0,SPEECH,"laugh.ogg")
	ADD_CREATURE_TO_LEVEL(PLAYER4,AVATAR,21,1,10,0,FALL)
	SET_FLAG(PLAYER2,FLAG3,3)
ENDIF

SET_CREATURE_HEALTH(AVATAR,7500)


IF(PLAYER2,FLAG3 == 3)
	IF(PLAYER4,AVATAR == 0)
		WIN_GAME
	ENDIF
ENDIF

REM SET_MUSIC("0")

IF(PLAYER0,TIMER0 >= 20)
	SET_MUSIC("easter_egg.ogg")
ENDIF

